#! @TCSH@ -f
#
# Copyright (C) 2000-2005 A. Marini and the SELF team 
#         http://www.fisica.uniroma2.it/~self
# 
# This file is distributed under the terms of the GNU 
# General Public License. You can redistribute it and/or 
# modify it under the terms of the GNU General Public 
# License as published by the Free Software Foundation; 
# either version 2, or (at your option) any later version.
#
# This program is distributed in the hope that it will 
# be useful, but WITHOUT ANY WARRANTY; without even the 
# implied warranty of MERCHANTABILITY or FITNESS FOR A 
# PARTICULAR PURPOSE.  See the GNU General Public License 
# for more details.
#
# You should have received a copy of the GNU General Public 
# License along with this program; if not, write to the Free 
# Software Foundation, Inc., 59 Temple Place - Suite 330,Boston, 
# MA 02111-1307, USA or visit http://www.gnu.org/copyleft/gpl.txt.
#
#==================================================
 set prefix  = @abs_top_srcdir@
 set bdir    = $prefix/../backup
 set xecho   = @MYECHO@
 set awk     = @AWK@
 set over    = @SVERSION@
 set opat    = @SPATCHLEVEL@
 set osub    = @SSUBLEVEL@
 set orev    = @SREVISION@
#==================================================
set what=$argv[1]
cd $prefix
set ver=$over
set pat=$opat
set sub=$osub
#
# Get revision
#
set revision_ch=`svn info | grep 'Revision'`
set rev=`echo $revision_ch | $awk '{gsub("Revision: ","");print $0}'`
#
# Increase counters
#
set sdirname  = "self-"$ver"."$pat"."$sub
if ( "$what" == "version" ) then
 @ ver ++
 set pat = 0
 set sub = 0
endif
if ( "$what" == "patchlevel" ) then
 @ pat ++
 set sub = 0
endif
if ( "$what" == "subversion" ) @ sub ++
#
# Check and create backup directory
#
set nsdirname  = "self-"$ver"."$pat"."$sub
set bsdir=$bdir/$ver"."$pat
set bsdir_last=$ver"."$pat
if ( ! -d $bdir  ) mkdir $bdir
if ( ! -d $bsdir ) mkdir $bsdir
set vstring=$ver"."$pat"."$sub
set fname_date=`date +%d-%m-%y`
set fname="self-"$vstring"_"$fname_date".tar"
#
# Version strings
$xecho 'code_version(1)='$ver >  include/version.inc
$xecho 'code_version(2)='$pat >> include/version.inc
$xecho 'code_version(3)='$sub >> include/version.inc
$xecho 'code_revision  ='$rev >> include/version.inc
$xecho 'build_date="'$fname_date'"' >> include/version.inc
#
# Prepare new configure script
cat << EOF > ss.awk
{
 gsub("$over\\\.$opat\\\.$osub r\\\.$orev","$ver.$pat.$sub r.$rev",\$0)
 gsub("SVERSION=\"$over\"","SVERSION=\"$ver\"",\$0)
 gsub("SPATCHLEVEL=\"$opat\"","SPATCHLEVEL=\"$pat\"",\$0)
 gsub("SSUBLEVEL=\"$osub\"","SSUBLEVEL=\"$sub\"",\$0)
 gsub("SREVISION=\"$orev\"","SREVISION=\"$rev\"",\$0)
 print \$0 > "configure.ac.new"
}
EOF
mv config/configure.ac .
$awk -f ss.awk configure.ac
mv configure.ac.new configure.ac
$xecho "[ ss ] Running Autoconf "
autoconf
mv configure.ac config/
rm -fr ss.awk autom4te.cache/
#
# Rename the src_top_dir
#
cd $prefix/..
if ( "$what" != "none" ) then
 mv $sdirname $nsdirname
endif
#
# Backup
#
tar -cf $fname $nsdirname
gzip $fname
set bsdir=$ver"."$pat
mv $fname.gz backup/$bsdir
cd backup ; rm -f self-latest.tar.gz ; ln -s $bsdir/$fname.gz self-latest.tar.gz
$xecho "[ ss ] SELF " $vstring "backuped"
